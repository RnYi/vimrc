""""""""""""""""
"  Var define  "
""""""""""""""""
if !exists('g:vimrc_path')
    if has('win32')
        let g:vimrc_path=expand("~/vimfiles")
    else
        let g:vimrc_path=expand("~/.vim")
    endif
endif

""""""""""""""
"  vim-plug  "
""""""""""""""
call plug#begin()
" Theme
Plug 'lifepillar/vim-solarized8'
" Colorized parentheses
Plug 'luochen1990/rainbow'
" Indent line
Plug 'Yggdroot/indentLine', {'on':'IndentLinesToggle'}
" Statusline
Plug 'itchyny/lightline.vim'
" Show buffers on tabline
Plug 'ap/vim-buftabline'
" Completion
Plug 'neoclide/coc.nvim', {'branch': 'release'}
Plug 'jackguo380/vim-lsp-cxx-highlight', {'for':['c','cpp']}
" Delimiter
Plug 'Raimondi/delimitMate'
" Function signature on command line
Plug 'Shougo/echodoc.vim'
" Markdown
Plug 'godlygeek/tabular'
Plug 'iamcco/markdown-preview.nvim', { 'do': { -> mkdp#util#install() }, 'for':['markdown','vim-plug']}
" Latex
" Plug 'lervag/vimtex', {'for':['tex','latex']}
" File explorer
Plug 'lambdalisue/fern.vim'
Plug 'lambdalisue/fern-git-status.vim'
if has('nvim')
    Plug 'antoinemadec/FixCursorHold.nvim'
endif
" Enhanced motion
Plug 'justinmk/vim-sneak'
" Start-up
Plug 'mhinz/vim-startify'
" Comment
Plug 'tpope/vim-commentary'
" Surround
Plug 'tpope/vim-surround'
" Fullscreen
if !has('nvim')
    Plug 'xolox/vim-misc'
    Plug 'xolox/vim-shell'
endif
" Snippets
Plug 'honza/vim-snippets'
Plug 'SirVer/ultisnips'
" Async
Plug 'skywind3000/asynctasks.vim', {'on':['AsyncTask', 'AsyncTaskMacro', 'AsyncTaskList', 'AsyncTaskEdit']}
Plug 'skywind3000/asyncrun.vim', {'on':['AsyncRun','AsyncStop']}
" Use jk to enter normal mod
Plug 'jdhao/better-escape.vim'
call plug#end()

"""""""""""
"  Basic  "
"""""""""""
set mouse-=a
set autoread
if !has('win32')
    set shellslash
endif
" set shortmess+=c
if !has('nvim')
    set nocompatible
endif
set updatetime=300
set backspace=indent,eol,start
syntax on
filetype on
filetype plugin on
filetype indent on
let g:mapleader="\<Space>"
augroup myaug
    autocmd!
    autocmd BufReadPost *
                \   if line("'\"") > 0 && line ("'\"") <= line("$") |
                \       exe "normal g'\"" |
                \   endif
    autocmd FileType help wincmd L
    autocmd FileType json syntax match Comment +\/\/.\+$+
augroup END

""""""""""""
"  Indent  "
""""""""""""
set tabstop=4
set smarttab
set expandtab
set shiftround
set autoindent
set smartindent
set shiftwidth=4
set softtabstop=4
augroup myaug
    autocmd FileType html,htmldjango setlocal tabstop=2 softtabstop=2 shiftwidth=2
augroup END

"""""""""""""
"  Display  "
"""""""""""""
set hidden
set number
set noshowcmd
set wrap
set linebreak
set breakat-=_
set wildmenu
set noshowmode
set cursorline
set scrolloff=1
set sidescrolloff=5
set laststatus=2
set showtabline=2
set guioptions=
set guioptions+=c
set termguicolors
set display=lastline
set formatoptions+=mMj
if has('gui_running')
    set lines=30
    set columns=85
    winpos 350 50
endif

""""""""""""
"  Search  "
""""""""""""
set wrapscan
set smartcase
set nohlsearch
set incsearch
set ignorecase
if has('nvim')
    set inccommand=nosplit
endif

""""""""""""""
"  Encoding  "
""""""""""""""
language en_US
set langmenu=en_US
set encoding=utf-8
set termencoding=utf-8
set fileencoding=utf-8
set guifont=MesloLGS\ NF:h14
set guifontwide=等距更纱黑体\ SC:h14
set fileencodings=utf-8,ucs-bom,chinese,gb18030,gbk,gb2312,cp936

"""""""""""""
"  Session  "
"""""""""""""
set viewoptions-=options
set sessionoptions-=options

"""""""""""
"  Theme  "
"""""""""""
set background=dark
let g:solarized_italics=0
colorscheme solarized8_flat

""""""""""
"  Fold  "
""""""""""
set foldmethod=manual

"""""""""""""""
"  Highlight  "
"""""""""""""""
augroup myaug
    autocmd FileType markdown hi Error NONE
augroup END

""""""""""""
"  Plugin  "
""""""""""""
" IndentLien
let g:indentLine_enabled=0
nnoremap <F2> <Cmd>IndentLinesToggle<CR>

" echodoc
let g:echodoc_enable_at_startup=1

" vim-lsp-cxx-highlight
let g:cpp_class_decl_highlight=1
let g:cpp_class_scope_highlight=1
let g:cpp_member_variable_highlight=1

" vim-sneak
let g:sneak#label=1
let g:sneak#use_ic_scs=1

" better-escape
let g:better_escape_interval = 100
let g:better_escape_shortcut=['jk','kj']

" vim-shell
if !has('nvim')
    let g:shell_mappings_enabled=0
    let g:shell_fullscrejen_message=0
    let g:shell_fullscreen_items='mT'
    let g:shell_fullscreen_always_on_top=0
    if has('gui_running') && has('win32')
        nnoremap <silent> <F1> <Cmd>Fullscreen<CR>
        inoremap <silent> <F1> <Cmd>Fullscreen<CR>
    endif
endif

" markdown-preview
let g:mkdp_auto_close=0
nmap <Leader>p <Plug>MarkdownPreviewToggle

" vimtex
let g:tex_flavor='tex'

" delimitMate
let delimitMate_expand_cr=1
let delimitMate_matchpairs=&matchpairs
let delimitMate_quotes="\" ' `"
augroup myaug
    autocmd FileType python let b:delimitMate_nesting_quotes=['"']
    autocmd FileType markdown,latex,tex let b:delimitMate_quotes=delimitMate_quotes." \$"
augroup END

" fern
let g:fern#smart_cursor="hide"
let g:fern#disable_default_mappings=1
let g:fern#disable_auto_buffer_delete=1
let g:fern#renderer#default#leaf_symbol="| "
let g:fern#renderer#default#expanded_symbol="\u25be "
let g:fern#renderer#default#collapsed_symbol="\u25b8 "
function! s:init_fern() abort
    setlocal nonu
    nmap <buffer><expr>
                \ <Plug>(fern-my-open-or-expand-or-collapse)
                \ fern#smart#leaf(
                \   "\<Plug>(fern-action-open)",
                \   "\<Plug>(fern-action-expand:stay)",
                \   "\<Plug>(fern-action-collapse)",
                \ )
    nmap <buffer><silent> e    <Plug>(fern-action-open:tabedit)
    nmap <buffer><silent> o    <Plug>(fern-my-open-or-expand-or-collapse)
    nmap <buffer><silent> go   <Plug>(fern-action-open:edit)<C-w>p
    nmap <buffer><silent> t    <Plug>(fern-action-open:tabedit)
    nmap <buffer><silent> T    <Plug>(fern-action-open:tabedit)gT
    nmap <buffer><silent> ma   <Plug>(fern-action-new-path)
    nmap <buffer><silent> mc   <Plug>(fern-action-copy)
    nmap <buffer><silent> md   <Plug>(fern-action-trash)
    nmap <buffer><silent> mm   <Plug>(fern-action-move)
    nmap <buffer><silent> mr   <Plug>(fern-action-rename)

    nmap <buffer><silent> cc   <Plug>(fern-action-clipboard-copy)
    nmap <buffer><silent> cp   <Plug>(fern-action-clipboard-paste)
    nmap <buffer><silent> cm   <Plug>(fern-action-clipboard-move)
    nmap <buffer><silent> cr   <Plug>(fern-action-clipboard-clear)
    nmap <buffer><silent> l    <Plug>(fern-action-enter)
    nmap <buffer><silent> h    <Plug>(fern-action-leave)
    nmap <buffer><silent> r    <Plug>(fern-action-reload)
    nmap <buffer><silent> x    <Plug>(fern-action-collapse)
    nmap <buffer><silent> R  gg<Plug>(fern-action-reload)<C-o>
    nmap <buffer><silent> cd   <Plug>(fern-action-tcd:cursor)
    nmap <buffer><silent> CD <Cmd>Fern . -drawer -keep<CR>

    nmap <buffer><silent> -    <Plug>(fern-action-mark:toggle)
    nmap <buffer><silent> _    <Plug>(fern-action-mark:clear)
    nmap <buffer><silent> I    <Plug>(fern-action-hidden:toggle)

    nmap <buffer><silent> q <Cmd>quit<CR>
endfunction
augroup fern-custom
    autocmd! *
    autocmd FileType fern call s:init_fern()
augroup END
nnoremap <silent> <Leader>e <Cmd>Fern . -drawer -keep -toggle<CR>

" Ultisnips
let g:UltiSnipsExpandTrigger="<c-j>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsJumpBackwardTrigger="<c-k>"
let g:UltiSnipsListSnippets="<c-l>"
inoremap <C-x><C-k> <C-x><C-k>

" startify
let g:startify_change_cmd='tcd'
let g:startify_session_sort = 1
let g:startify_files_number = 5
let g:startify_custom_header = []
let g:startify_session_number = 5
let g:startify_update_oldfiles = 1
let g:startify_session_persistence=1
let g:startify_session_delete_buffers=1
let g:startify_lists=[
            \   { 'type': 'sessions',  'header': ['   Sessions']       },
            \   { 'type': 'files',     'header': ['   MRU']            },
            \]

" vim-commentary
augroup myaug
    autocmd FileType json setlocal commentstring=//%s
augroup END

" AsyncTasks
augroup myaug
    autocmd BufRead,BufNewFile .tasks set filetype=tasks
    autocmd BufRead,BufNewFile tasks.ini set filetype=tasks
augroup END
let g:asyncrun_open=6
let g:asynctasks_term_pos='right'
let g:asynctasks_term_reuse=1
let g:asynctasks_extra_config=[
            \ join([g:vimrc_path,"tasks.ini"],'/'),
            \ ]
let g:asyncrun_rootmarks=['.root','.project','.git','.hg','.svn','.projections.json']
nnoremap <silent> <F3> <Cmd>AsyncTask run<CR>
nnoremap <silent> <F4> <Cmd>AsyncTask build<CR>
nnoremap <silent> <F5> <Cmd>AsyncTask debug<CR>
inoremap <silent> <F3> <Cmd>AsyncTask run<CR>
inoremap <silent> <F4> <Cmd>AsyncTask build<CR>
inoremap <silent> <F5> <Cmd>AsyncTask debug<CR>

" Rainbow Parentheses
let g:rainbow_active=1
let clr_st=&bg==#'dark'?'white':'black'
let g:rainbow_conf={
            \   'guifgs':[clr_st,'gold','chocolate','magenta','OrangeRed'],
            \   'operators': '_,_',
            \   'parentheses': ['start=/(/ end=/)/ fold',
            \                   'start=/\[/ end=/\]/ fold',
            \                   'start=/{/ end=/}/ fold'],
            \   'separately': {
            \       '*': {},
            \       'nerdtree':0,
            \       'tex': {
            \           'parentheses': ['start=/(/ end=/)/', 'start=/\[/ end=/\]/'],
            \       },
            \       'lisp': {
            \           'guifgs': ['darkorange3', 'seagreen3', 'royalblue3', 'firebrick'],
            \       },
            \       'vim': {
            \           'parentheses':
            \               ['start=/(/ end=/)/',
            \                'start=/\[/ end=/\]/',
            \                'start=/{/ end=/}/ fold',
            \                'start=/(/ end=/)/ containedin=vimFuncBody',
            \                'start=/\[/ end=/\]/ containedin=vimFuncBody',
            \                'start=/{/ end=/}/ fold containedin=vimFuncBody'],
            \       },
            \       'html': {
            \           'parentheses': ['start=/\v\<((area|base|br|col|embed|hr|img|input|keygen|link|menuitem|meta|param|source|track|wbr)[ >])@!\z([-_:a-zA-Z0-9]+)(\s+[-_:a-zA-Z0-9]+(\=("[^"]*"|'."'".'[^'."'".']*'."'".'|[^ '."'".'"><=`]*))?)*\>/ end=#</\z1># fold'],
            \       },
            \       'css': 0,
            \   }
            \}

" lightline
let g:lightline={
            \   'colorscheme':'solarized',
            \   'active' : {
            \       'left': [['mode', 'paste'],['filename'],['cocstatus']],
            \       'right' : [['filetype'],['lineinfo']]
            \   },
            \   'inactive':{
            \       'left':[['relativepath']],
            \       'right' : [['filetype']]
            \},
            \   'enable':{
            \       'statusline':1,
            \       'tabline':0
            \   },
            \   'component_function': {
            \       'mode':'LightlineMode',
            \       'cocstatus':'coc#status',
            \       'filename':'LightlineFilename',
            \       'filetype':'LightlineFiletype',
            \       'lineinfo':'LightlineLineinfo',
            \   },
            \}
function! LightlineMode()
    return &filetype ==? 'fern' ? 'Fern' :
                \          &filetype ==? 'startify' ? 'Startify' :
                \          lightline#mode()
endfunction

function! LightlineFilename()
    let l:filename=expand('%:t')
    return &filetype ==? 'fern' ? '' :
                \          &filetype ==? 'startify' ? '' :
                \          &filetype == '' ? '' :
                \          winwidth('%') < 40 ? '' : l:filename
endfunction

" function! LightlinePath()
    " let l:rlpath=expand('%')
    " return &filetype ==? 'fern' ? '' :
                " \          &filetype ==? 'startify' ? '' :
                " \          &filetype ==? 'help' ? expand('%:t') :
                " \          &filetype == '' ? '' :
                " \          winwidth('%') < 40 ? '' :
                " \          strchars(l:rlpath) < 20 ? l:rlpath : pathshorten(l:rlpath)
" endfunction

function! LightlineFiletype()
    return &filetype ==? 'fern' ? '' :
                \          &filetype ==? 'startify' ? '' :
                \          winwidth('%') > 40 ? &filetype : ''
endfunction

function! LightlineLineinfo()
    return &filetype ==? 'fern' ? '' :
                \          &filetype ==? 'startify' ? '' :
                \          winwidth('%') > 40 ? printf("%d/%d", line('.'),line('$')) : ''
endfunction

" vim-buftabline
let g:buftabline_numbers=2
let g:buftabline_indicators=1
if has('nvim')
    let g:buftabline_separators=1
endif
nnoremap <silent> <TAB> <Cmd>bn<CR>
nnoremap <silent> <S-TAB> <Cmd>bp<CR>
nnoremap <silent> <Leader>d <Cmd>bd<CR>
nmap <leader>1 <Plug>BufTabLine.Go(1)
nmap <leader>2 <Plug>BufTabLine.Go(2)
nmap <leader>3 <Plug>BufTabLine.Go(3)
nmap <leader>4 <Plug>BufTabLine.Go(4)
nmap <leader>5 <Plug>BufTabLine.Go(5)
nmap <leader>6 <Plug>BufTabLine.Go(6)
nmap <leader>7 <Plug>BufTabLine.Go(7)
nmap <leader>8 <Plug>BufTabLine.Go(8)
nmap <leader>9 <Plug>BufTabLine.Go(9)
nmap <leader>0 <Plug>BufTabLine.Go(10)

" coc.nvim
let g:coc_config_home=g:vimrc_path
let g:markdown_fenced_languages= ['vim','help','css', 'js=javascript']
let g:coc_global_extensions=["coc-clangd","coc-json","coc-vimlsp","coc-cmake","coc-tasks","coc-pyright","coc-html","coc-ultisnips","coc-vimtex"]
command! -nargs=0 Format :call CocAction('format')
command! -nargs=? Fold :call CocAction('fold', <f-args>)
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-refrences)
nmap <Leader>rn <Plug>(coc-rename)
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)
nnoremap <silent> K <Cmd>call <SID>show_documentation()<CR>
nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
inoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(1)\<cr>" : "\<Right>"
inoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? "\<c-r>=coc#float#scroll(0)\<cr>" : "\<Left>"
function! s:show_documentation()
    if (index(['vim','help'], &filetype) >= 0)
        execute 'h '.expand('<cword>')
    elseif (coc#rpc#ready())
        call CocActionAsync('doHover')
    else
        execute '!' . &keywordprg . " " . expand('<cword>')
    endif
endfunction
inoremap <silent><expr> <TAB>
            \ pumvisible() ? coc#_select_confirm() :
            \ <SID>check_back_space() ? "\<TAB>" :
            \ coc#refresh()

function! s:check_back_space() abort
    let col = col('.') - 1
    return !col || getline('.')[col - 1]  =~# '\s'
endfunction
hi CocErrorHighlight gui=undercurl guisp=#ff0000
hi CocWarningHighlight gui=undercurl guisp=#ff922b
hi CocInfoHighlight gui=undercurl guisp=#fab005
hi CocHintHighlight gui=undercurl guisp=#15aabf

"""""""""""""""""
"  Key mapping  "
"""""""""""""""""
noremap H ^
noremap L $

nnoremap n nzz
nnoremap N Nzz

nnoremap <M-j> gj
nnoremap <M-k> gk

nnoremap <M-J> <C-w>j
nnoremap <M-K> <C-w>k
nnoremap <M-H> <C-w>h
nnoremap <M-L> <C-w>l
nnoremap <M-Q> <C-w>q
inoremap <M-J> <ESC><C-w>j
inoremap <M-K> <ESC><C-w>k
inoremap <M-H> <ESC><C-w>h
inoremap <M-L> <ESC><C-w>l
inoremap <M-Q> <ESC><C-w>q

inoremap <M-j> <Down>
inoremap <M-k> <Up>
inoremap <M-h> <Left>
inoremap <M-l> <Right>

cnoremap <M-h> <Left>
cnoremap <M-l> <Right>

inoremap <M-CR> <ESC>o
inoremap <S-CR> <ESC>O

map <MiddleMouse> <Nop>
imap <MiddleMouse> <Nop>

noremap <C-space> <ESC>
noremap! <C-space> <ESC>
tnoremap <C-space> <ESC>

nnoremap <silent> <M-s> <Cmd>update<CR>
inoremap <silent> <M-s> <Cmd>update<CR>

nnoremap <silent><expr> q &filetype==#'help'?":bd\<CR>":'q'

if has('nvim')
    tnoremap <M-J> <C-\><C-N><C-w>j
    tnoremap <M-K> <C-\><C-N><C-w>k
    tnoremap <M-H> <C-\><C-N><C-w>h
    tnoremap <M-L> <C-\><C-N><C-w>l
    tnoremap <M-Q> <C-\><C-N><C-w>q
    tnoremap <M-q> <C-\><C-N>
    vnoremap <silent> <C-Insert> "+y
    vnoremap <silent> <S-Insert> "+p
    inoremap <silent> <S-Insert> <C-r>+
    nnoremap <silent> <S-Insert> "+p
    nnoremap <silent> <F1> <Cmd>call FullScreenToggle()<CR>
    inoremap <silent> <F1> <Cmd>call FullScreenToggle()<CR>
    tnoremap <silent> <F1> <Cmd>call FullScreenToggle()<CR>
    function! FullScreenToggle() abort
        if g:GuiWindowFullScreen
            call GuiWindowFullScreen(0)
        else
            call GuiWindowFullScreen(1)
        endif
    endfunction
    augroup myaug
        autocmd TermOpen * setlocal nonumber | startinsert
        autocmd SessionLoadPost * let g:GuiWindowFullScreen=0
    augroup END
else
    tnoremap <M-q> <C-w>N
    tnoremap <M-J> <C-w>j
    tnoremap <M-K> <C-w>k
    tnoremap <M-H> <C-w>h
    tnoremap <M-L> <C-w>l
    tnoremap <M-Q> <C-w>q
endif
